// Gradle이 빌드되기 전 실행되는 설정
buildscript {
	ext {
		springBootVersion = '2.4.5'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
	}
}

// 현재 root 프로젝트와 서브 모듈에 대한 설정
allprojects {}

// 전체 서브 모듈에 해당되는 설정
subprojects {
	apply plugin: 'java-library'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	group = 'com.lezhin'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = '11'

	repositories {
		mavenCentral()
	}

	dependencies {
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'


		implementation group: 'io.springfox', name: 'springfox-boot-starter', version: '3.0.0'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}

// api 모듈에 domain의 의존성을 추가
project(':api') {
	dependencies {
		implementation project(':domain')
	}
}